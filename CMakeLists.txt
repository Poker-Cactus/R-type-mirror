cmake_minimum_required(VERSION 3.15)
project(RType VERSION 1.0.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Export compile commands for IntelliSense
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# Cross-platform compile options
if(MSVC)
    # MSVC-specific flags
    add_compile_options(
        /W4                 # Warning level 4
        /permissive-        # Strict standard conformance
        /Zc:__cplusplus     # Enable correct __cplusplus macro
    )
    # Debug information for MSVC
    add_compile_options($<$<CONFIG:Debug>:/Zi>)
else()
    # GCC/Clang flags
    add_compile_options(
        -Wall
        -Wextra
    )
    # Debug information for GCC/Clang
    add_compile_options($<$<CONFIG:Debug>:-g3>)
endif()

## Conan dependencies
# Allow CMake to find packages generated by Conan
list(APPEND CMAKE_MODULE_PATH "${CMAKE_BINARY_DIR}")
find_package(SDL2 REQUIRED)
find_package(SDL2_ttf REQUIRED)
find_package(SDL2_image REQUIRED)
find_package(SDL2_mixer REQUIRED)
find_package(asio REQUIRED)
find_package(CapnProto REQUIRED)
find_package(doctest REQUIRED)
find_package(nlohmann_json REQUIRED)

# Add subdirectories
add_subdirectory(engineCore)
add_subdirectory(common)
add_subdirectory(network)
add_subdirectory(server)
add_subdirectory(client)

# Asset Editor (optional tool)
option(BUILD_ASSET_EDITOR "Build the Asset Editor tool" OFF)
if(BUILD_ASSET_EDITOR)
    add_subdirectory(assetEditor)
endif()



find_program(CLANG_TIDY_EXE NAMES clang-tidy)
if(CLANG_TIDY_EXE)
    message(STATUS "clang-tidy found: ${CLANG_TIDY_EXE}")
    set(CMAKE_CXX_CLANG_TIDY "${CLANG_TIDY_EXE}")
endif()
